grammar org.osate.xtext.aadl2.properties.Properties with org.eclipse.xtext.common.Terminals

//generate properties "http://www.osate.org/aadl2/properties/Properties"


import "platform:/resource/org.osate.aadl2/model/aadl2.ecore" as aadl2

PModel returns aadl2::Element:
	PropertySet | ContainedPropertyAssociation | BasicPropertyAssociation | PropertyAssociation;


// Properties

PropertySet returns aadl2::PropertySet:
	'property' 'set' name=ID 'is'
	(=> 'with' (importedUnit+=[aadl2::PropertySet|ID])  
	    (',' importedUnit+=[aadl2::PropertySet|ID])* ';')*
	(ownedPropertyType+=PropertyType | ownedProperty+=PropertyDefinition 
	| ownedPropertyConstant+=PropertyConstant)*
	'end' ID ';' ;

PropertyType returns aadl2::PropertyType:
	(BooleanType|StringType|EnumerationType|UnitsType |RealType
	|IntegerType|RangeType|ClassifierType|ReferenceType|RecordType) 
	';';

UnnamedPropertyType returns aadl2::PropertyType:
	(ListType|UnnamedBooleanType|UnnamedStringType|UnnamedEnumerationType|UnnamedUnitsType |UnnamedRealType
	|UnnamedIntegerType|UnnamedRangeType|UnnamedClassifierType|UnnamedReferenceType|UnnamedRecordType) 
	;


BooleanType returns aadl2::AadlBoolean:
	 name=ID ':' 'type' 
	'aadlboolean';


UnnamedBooleanType returns aadl2::AadlBoolean:
	{aadl2::AadlBoolean}'aadlboolean';

StringType returns aadl2::AadlString:
	name=ID ':' 'type' 
	'aadlstring';

UnnamedStringType returns aadl2::AadlString:
	{aadl2::AadlString}'aadlstring';

EnumerationType returns aadl2::EnumerationType:
	name=ID ':' 'type' 
	'enumeration'
	'(' ownedLiteral+=EnumerationLiteral (',' ownedLiteral+=EnumerationLiteral)* ')';

UnnamedEnumerationType returns aadl2::EnumerationType:
	'enumeration'
	'(' ownedLiteral+=EnumerationLiteral (',' ownedLiteral+=EnumerationLiteral)* ')';

EnumerationLiteral returns aadl2::EnumerationLiteral:
	name=ID;

UnitsType returns aadl2::UnitsType:
	name=ID ':' 'type' 
	'units'
	'(' ownedLiteral+=UnitLiteral (',' ownedLiteral+=UnitLiteralConversion)* ')';

UnnamedUnitsType returns aadl2::UnitsType:
	'units'
	'(' ownedLiteral+=UnitLiteral (',' ownedLiteral+=UnitLiteralConversion)* ')';

UnitLiteral returns aadl2::UnitLiteral:
	name=ID;

UnitLiteralConversion returns aadl2::UnitLiteral:
	name=ID '=>' baseUnit=[aadl2::UnitLiteral|ID] STAR factor=NumberValue
	;

RealType returns aadl2::AadlReal:
	name=ID ':' 'type' 
	'aadlreal' (range=RealRange)? ( (ownedUnitsType=UnnamedUnitsType| ('units' unitsType=[aadl2::UnitsType|QPREF])))?
	 ;

UnnamedRealType returns aadl2::AadlReal:
	{aadl2::AadlReal}
	'aadlreal' (range=RealRange)? ((ownedUnitsType=UnnamedUnitsType| ('units' unitsType=[aadl2::UnitsType|QPREF])))?
	 ;

IntegerType returns aadl2::AadlInteger:
	name=ID ':' 'type' 
	'aadlinteger' (range=IntegerRange)? ((ownedUnitsType=UnnamedUnitsType| ('units' unitsType=[aadl2::UnitsType|QPREF])))?
	 ;

UnnamedIntegerType returns aadl2::AadlInteger:
	{aadl2::AadlInteger}
	'aadlinteger' (range=IntegerRange)? ( (ownedUnitsType=UnnamedUnitsType| ('units' unitsType=[aadl2::UnitsType|QPREF])))?
	 ;

RangeType returns aadl2::RangeType:
	name=ID ':' 'type' 
	'range' 'of' (ownedNumberType=(UnnamedIntegerType|UnnamedRealType)
		| numberType=[aadl2::NumberType|QPREF]
	)
	 ;

UnnamedRangeType returns aadl2::RangeType:
	{aadl2::RangeType}
	'range' 'of' (ownedNumberType=(UnnamedIntegerType|UnnamedRealType)
		| numberType=[aadl2::NumberType|QPREF]
	)
	 ;

ClassifierType returns aadl2::ClassifierType:
	name=ID ':' 'type' 
	'classifier' 
	('(' classifierReference+=QCReference (',' classifierReference+=QCReference)*')')?
	 ;

UnnamedClassifierType returns aadl2::ClassifierType:
	{aadl2::ClassifierType}
	'classifier' 
	('(' classifierReference+=QCReference (',' classifierReference+=QCReference)*')')?
	 ;

QCReference returns aadl2::MetaclassReference:
	('{'  annexName=ID '}' STAR STAR )?
	( metaclassName+=(CoreKeyWord|ID) )+
	 ;

CoreKeyWord :
	'system' | 'thread' | 'group' | 'process' | 'data' | 'processor' | 'memory' | 'device' | 'bus' 
	 | 'virtual' | 'subprogram' | 'feature' |'package'|'parameter'
	| 'port' |'calls' |'event'|'flow'|'implementation'|'type'|'mode'|'prototype'|'to' |'end'
	| 'abstract' |'access'|'classifier'
	// the next ones are filtered as they could be used in an annex
//	 |'reference' | 'connections' |'constant'|'delta'
//	| 'and' | 'all' | 'annex' | 'applies' |'binding'|'boolean'|'enumeration'|'path'|'private'
//	|'extends'|'false'|'features'|'flows'|'in'|'inherit'|'initial'|'integer'|'inverse'|'is'|'list'|'modes'|'none'
//	|'not'
	// more to come if we have annex parsers 
;

ReferenceType returns aadl2::ReferenceType:
	name=ID ':' 'type' 
	'reference' 
	('(' namedElementReference+=QCReference (',' namedElementReference+=QCReference)* ')')?
	 ;

UnnamedReferenceType returns aadl2::ReferenceType:
	'reference' {aadl2::ReferenceType}
	('(' namedElementReference+=QCReference (',' namedElementReference+=QCReference)* ')')?
	 ;

RecordType returns aadl2::RecordType:
	name=ID ':' 'type' 
	'record' '('
	(ownedField+=RecordField )+
	')'
	 ;

UnnamedRecordType returns aadl2::RecordType:
	'record' '('
	(ownedField+=RecordField )+
	')'
	 ;

RecordField returns aadl2::BasicProperty:
	name=ID ':'
//	('list' 'of')* is handled as part of UnnamedPropertytype
	(propertyType=[aadl2::PropertyType|QPREF]|ownedPropertyType=UnnamedPropertyType)
	';'
	 ;

//&&&&& Property Definition


PropertyDefinition returns aadl2::Property:
	name=ID ':'
	(inherit?='inherit')?
//	('list' 'of')* is handled as part of UnnamedPropertytype
	(propertyType=[aadl2::PropertyType|QPREF]|ownedPropertyType=UnnamedPropertyType)
	('=>' defaultValue=PropertyExpression)?
	'applies' 'to' '('
	((appliesTo+=PropertyOwner (',' appliesTo+=PropertyOwner)*)|
	appliesTo+=AllReference)
	')'
	';'
	 ;

AllReference returns aadl2::MetaclassReference:
	 metaclassName+='all'  // &&&&& actually set it to NamedElement using ALL rule returning "named element" as string
;

ListType returns aadl2::ListType:
	 'list' 'of'  
	(elementType=[aadl2::PropertyType|QPREF]|ownedElementType=UnnamedPropertyType)
;


PropertyOwner returns aadl2::PropertyOwner:
	// classifier value is always qualified by a package name
	QCReference
	 ;



PropertyConstant returns aadl2::PropertyConstant:
	name=ID ':' 'constant'
//	('list' 'of')* is handled as part of UnnamedPropertytype
	(propertyType=[aadl2::PropertyType|QPREF]|ownedPropertyType=UnnamedPropertyType)
	'=>' constantValue=ConstantPropertyExpression  
	';'
	 ;




NumberValue returns aadl2::NumberValue:
	RealLit | IntegerLit ;

RealLit returns aadl2::RealLiteral:
	value=SignedReal ;


IntegerLit returns aadl2::IntegerLiteral:
	value=SignedInt ;

BasicPropertyAssociation returns aadl2::PropertyAssociation:
	property=[aadl2::Property|QPREF] 
	'=>' ownedValue+=PropertyValue ';'
	;

ContainedPropertyAssociation returns aadl2::PropertyAssociation:
	property=[aadl2::Property|QPREF] 
	('=>' | append?='+=>') (constant?='constant')?
	(( =>
	'(' ownedValue+=ModalPropertyValue 
	(',' ownedValue+=ModalPropertyValue)*
	//(',' ownedValue+=PropertyValue)?
	')' )
	| ownedValue+=PropertyValue
	)
	(=> 'applies' 'to' appliesTo+=ContainmentPath 
		(',' appliesTo+=ContainmentPath)*
	)?
	';';

PropertyAssociation returns aadl2::PropertyAssociation:
	property=[aadl2::Property|QPREF] 
	('=>' | append?='+=>') (constant?='constant')?
	(( 
	'(' ( ownedValue+=ModalPropertyValue => ',')*
//	(',' ownedValue+=ModalPropertyValue)*
//	(',' ownedValue+=PropertyValue)?
	ownedValue+=OptionalModalPropertyValue
	')' )
	| ownedValue+=PropertyValue
	)
	';';


ContainmentPath returns aadl2::ContainedNamedElement:
	{aadl2::ContainedNamedElement}
	containmentPathElement+=ContainmentPathElement
	( '.' containmentPathElement+=ContainmentPathElement)*
;

ModalPropertyValue returns aadl2::ModalPropertyValue:
	ownedValue=PropertyExpression 
	// phf made this optional: need to check separately that only the last one is optional
	=> 'in' 'modes' '(' 
	inMode+=[aadl2::Mode|ID] (',' inMode+=[aadl2::Mode|ID])*
	')'
	;

OptionalModalPropertyValue returns aadl2::ModalPropertyValue:
	ownedValue=PropertyExpression 
	// phf made this optional: need to check separately that only the last one is optional
	( 'in' 'modes' '(' 
	inMode+=[aadl2::Mode|ID] (',' inMode+=[aadl2::Mode|ID])*
	')')?
	;
	// &&&&&&&&&& handling of in binding

PropertyValue returns aadl2::ModalPropertyValue:
	ownedValue=PropertyExpression 
	;

PropertyExpression returns aadl2::PropertyExpression:
	  RecordTerm |NumericRangeTerm
	| ReferenceTerm | ComputedTerm | StringTerm 
	| RealTerm | IntegerTerm 
	 | ComponentClassifierTerm 	| ListTerm | BooleanTerm
	  | LiteralorReferenceTerm
	;

ConstantPropertyExpression returns aadl2::PropertyExpression:
	 RecordTerm |NumericRangeTerm
	| ComputedTerm | StringTerm 
	| RealTerm | IntegerTerm 
	 | ComponentClassifierTerm 	| ListTerm | BooleanTerm
	  | LiteralorReferenceTerm
	;

LiteralorReferenceTerm returns aadl2::NamedValue:
  namedValue=[aadl2::AbstractNamedValue|QPREF]
;



BooleanTerm returns aadl2::PropertyExpression:
	OrTerm ;

  
OrTerm returns aadl2::PropertyExpression:
  AndTerm ( {aadl2::Operation.ownedPropertyExpression+=current} op=OrOp ownedPropertyExpression+=AndTerm)*;


AndTerm returns aadl2::PropertyExpression:
  BooleanAtom ({aadl2::Operation.ownedPropertyExpression+=current} op=AndOp ownedPropertyExpression+=BooleanAtom)*;


NotTerm returns aadl2::Operation:
  op=NotOp ownedPropertyExpression+=BooleanAtom;



BooleanAtom returns aadl2::PropertyExpression:
	( BooleanLiteral  | NotTerm //| ConstantValue
	| ('[' BooleanTerm ']')
	);

BooleanLiteral returns aadl2::BooleanLiteral:
  {aadl2::BooleanLiteral}(value?='true'|'false')
  //Assumes: default for value is false
  ;

ConstantValue returns aadl2::NamedValue:
  namedValue=[aadl2::AbstractNamedValue|QPREF]
  ;

ReferenceTerm returns aadl2::ReferenceValue:
  'reference' '(' 	
  containmentPathElement+=ContainmentPathElement
	( '.' containmentPathElement+=ContainmentPathElement)*
  ')'
  ;

RecordTerm returns aadl2::RecordValue:
   '('
   (ownedFieldValue+=FieldPropertyAssociation)+
  ')'
  ;

ComputedTerm returns aadl2::ComputedValue:
   'compute' '(' 	
   function=ID
  ')'
  ;

ComponentClassifierTerm returns aadl2::ClassifierValue:
   'classifier' '(' 	
   classifier=[aadl2::ComponentClassifier|QCREF]
  ')'
  ;


ListTerm returns aadl2::ListValue:
   '(' 	
   ownedListElement+=(PropertyExpression)
   (',' ownedListElement+=(PropertyExpression))*
  ')'
  ;

FieldPropertyAssociation returns aadl2::BasicPropertyAssociation:
	property=[aadl2::BasicProperty|ID] 
	'=>' 
	ownedValue=PropertyExpression 
	';';

// from AADL2
// need to add annex path element
ContainmentPathElement returns aadl2::ContainmentPathElement:
	((namedElement=[aadl2::NamedElement|ID]
	 (arrayRange+=ArrayRange)?
	 ) |
	 'annex' namedElement=[aadl2::NamedElement|ANNEXREF]
	)
;

ANNEXREF :
	// check what values are ok inside ** **
	'{' STAR STAR ID STAR STAR '}';

  


enum OperationKind returns aadl2::OperationKind:
	 and='and' | or='or' | not='not' | plus='+' | minus = '-';
// Need Value Converters	
AndOp returns aadl2::OperationKind: 'and';
OrOp returns aadl2::OperationKind: 'or';
NotOp returns aadl2::OperationKind: 'not';
PlusMinus returns aadl2::OperationKind: '+' | '-';


StringTerm returns aadl2::StringLiteral:
	value=NoQuoteString ;


NoQuoteString :
	// remove quotes from string in ValueConverter
	STRING
;


ArrayRange returns aadl2::ArrayRange: {aadl2::ArrayRange}
	'[' lowerBound=gINT ('..' upperBound=gINT)? 
	']'
;

IntegerRange returns aadl2::NumericRange:
	lowerBound=(IntegerTerm | SignedConstant | ConstantValue )
	'..' upperBound=(IntegerTerm | SignedConstant | ConstantValue )
	;

RealRange returns aadl2::NumericRange:
	lowerBound=(RealTerm | SignedConstant | ConstantValue )
	'..' upperBound=(RealTerm | SignedConstant | ConstantValue )
	;

SignedConstant returns aadl2::Operation:
	op=PlusMinus ownedPropertyExpression+=ConstantValue
	;

IntegerTerm returns aadl2::IntegerLiteral:
	value=SignedInt (unit=[aadl2::UnitLiteral|ID])?
	;

SignedInt returns aadl2::Integer:
	// in O2 parser: minus sign handled strangely
	// Need valueconverter
	('+'|'-')?INT ;
	
// IntegerLiteral allowing underscore in teh middle of the digits

RealTerm returns aadl2::RealLiteral:
	value=SignedReal (unit=[aadl2::UnitLiteral|ID])?
	// or constant is not handled in O2 grammar
	// valueString not set
	;

SignedReal returns aadl2::Real:
	// in O2 parser: minus sign handled strangely
	// Need valueconverter
	('+'|'-')?REAL ;


NumericRangeTerm returns aadl2::RangeValue:
	minimum=(RealTerm|IntegerTerm| SignedConstant | ConstantValue)  
		'..' maximum=(RealTerm|IntegerTerm| SignedConstant | ConstantValue)
	('delta' delta=(RealTerm|IntegerTerm| SignedConstant | ConstantValue))?
	;

terminal SL_COMMENT:
	'--' !('\n' | '\r')* ('\r'? '\n')?;


REAL:
	INT '.' INT;

gINT returns aadl2::Integer:
	INT 	
;

gREAL returns aadl2::Real:
	REAL 	
;


QPREF:
	ID ('::' ID)?;

QCREF:
	(ID '::')* ID ('.' ID)?;

EQCREF:
	(ID '::')+ ID ('.' ID)?;
	
STAR : '*';
