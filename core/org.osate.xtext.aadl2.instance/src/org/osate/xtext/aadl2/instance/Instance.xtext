grammar org.osate.xtext.aadl2.instance.Instance hidden(WS, SL_COMMENT)

import "http://aadl.info/AADL/2.0/instance" as instance
import "http://aadl.info/AADL/2.0" as aadl2

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

SystemInstance returns instance::SystemInstance:
	category=ComponentCategory name=ID ':' componentImplementation=[aadl2::ComponentImplementation|IMPLREF] '{'
		(componentInstance+=ComponentInstance | systemOperationMode+=SystemOperationMode)*
	'}'
;
	
ComponentInstance returns instance::ComponentInstance:
	category=ComponentCategory name=ID ':' subcomponent=[aadl2::Subcomponent|SUBREF] '[' index+=LONG (',' index+=LONG)* ']' ('{'
		componentInstance+=ComponentInstance*
	'}')?
;
	
SystemOperationMode returns instance::SystemOperationMode:
	'som' name=STRING
;

ComponentCategory returns aadl2::ComponentCategory: 'abstract' | 'bus'|'data'
	| 'device' | 'memory' | 'process' | 'processor' | 'subprogram'
	| 'subprogram' 'group' | 'system' | 'thread' 'group'
	| 'thread' | 'virtual' 'bus' | 'virtual' 'processor';

IMPLREF:
	(ID '::')+ ID '.' ID
;
	
SUBREF:
	(ID '::')+ ID '.' ID '::' ID
;
	
terminal ID:	('a'..'z'
        |'A'..'Z'
        ) ( ('_')? ('a'..'z'
        |'A'..'Z'
        |'0'..'9'))*;

terminal LONG returns ecore::ELong:
	('0' .. '9')+
;

terminal STRING	: 
			'"' ( '\\' ('b'|'t'|'n'|'f'|'r'|'u'|'"'|"'"|'\\') | !('\\'|'"') )* '"' |
			"'" ( '\\' ('b'|'t'|'n'|'f'|'r'|'u'|'"'|"'"|'\\') | !('\\'|"'") )* "'"
		; 

terminal SL_COMMENT:
	'--' !('\n' | '\r')* ('\r'? '\n')?;
	

terminal WS			: (' '|'\t'|'\r'|'\n')+;


